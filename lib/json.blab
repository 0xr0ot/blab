
json = json.exp

json { # json.exp is an object

   # make an object which likely has at least one member
   exp = nonemptyobject | nonemptyobject | nonemptyobject | object

   nonemptyobject = "{" members "}"

   pair = string W ":"  value

   members = pair 
           | pair W "," W members

   sign = "" | "+" | "-"

   digit = [0-9]

   int = digit
       | [1-9] digits
       | "-" digit
       | "-" [1-9] digits

   number = int
          | int frac
          | int exponent
          | int frac exponent

   digits = digit 
          | digit digits 

   frac = "." digits

   exponent = e digits

   e = [eE] ("" | "+" | "-")

   char = [a-z] | [a-zA-Z] | special-char | [-0-9 +.:]  

   chars = char | char chars

   valid-charpoint = [0-9a-ce][0-9a-f]{3} 
                   | "d" [0-7][0-9a-f]{2}
                   | "f" [0-9a-c][0-9a-f]{2}   

   special-char = "\\u" valid-charpoint
                | "\\" ["\/bfnrt]
                | [λα∊∀→←↔▸∃∀☃]

   string = "\"" chars "\"" 
          | "\"" char+ "\"" 
          | "\"\""

   ws = 9 | 10 | 32

   W = "" | ("" | ("" | ("" | ws* )))

   array = "[]" 
         | "[" elements "]"

   elements = value 
            | value W "," W elements

   immediate = "true" | "false" | "null"

   object = "{" members "}" 
          | "{}"

   value = string
         | number
         | object 
         | array
         | immediate
         | W value W

}
